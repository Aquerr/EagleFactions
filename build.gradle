buildscript {
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id 'org.spongepowered.plugin' version '0.9.0'
}

allprojects {
    description = 'A factions plugin that will make managing your battle-server easier. :)'
    group = "io.github.aquerr"
    version = "0.14.0"

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    implementation project(":EagleFactionsAPI")
    implementation project(":common")
    implementation project(":sponge-api5")
    implementation project(":sponge-api6")
    implementation project(":sponge-api7")

    testImplementation project(':EagleFactionsAPI')
    testImplementation 'org.spongepowered:spongeapi:7.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation 'org.mockito:mockito-core:3.2.4'
}

shadowJar {
    if(System.getenv("JENKINS_HOME")) {
        version = version + "_" + System.getenv("BUILD_NUMBER")
        println("File name => " + baseName)
    } else {
        version = version + "-SNAPSHOT"
    }
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

tasks.create("publishBuildOnDiscord") {
    group = "Publishing"
    description = "Task for publishing the jar file to discord's jenkins channel"
    doLast {
        String[] jarFiles = new FileNameFinder().getFileNames(project.buildDir.getPath(), "**/*.jar")

        if(jarFiles.length > 0) {
            println("Found jar files: " + jarFiles)

            String lastCommitDescription = "git log -1 --pretty=%B".execute().getText()
            if(lastCommitDescription == null || lastCommitDescription == "") {
                lastCommitDescription = "No changelog provided"
            }

            exec {
                commandLine("java", "-jar", "..\\jenkinsdiscordbot-1.0.jar", "EagleFactions", jarFiles[0], lastCommitDescription)
            }
        }
    }
}