plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.spongepowered.plugin' version '0.9.0'
    id "net.kyori.blossom" version "1.1.0"
}

allprojects {
    description = 'A factions plugin that will make managing your battle-server easier. :)'
    group = "io.github.aquerr"
    version = "0.16.0"

    project.tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url 'https://jitpack.io' }
        maven { url "https://raw.github.com/FabioZumbi12/UltimateChat/mvn-repo/" }
    }
}

dependencies {
    implementation project(":EagleFactionsAPI")
    compileOnly 'org.spongepowered:spongeapi:7.3.0'
    compileOnly 'com.github.rojo8399:PlaceholderAPI:4.5.1'
    compileOnly 'org.mariadb.jdbc:mariadb-java-client:2.6.0'
    compileOnly 'com.zaxxer:HikariCP:2.6.3'
    compileOnly 'com.h2database:h2:1.4.196'
    compileOnly 'org.xerial:sqlite-jdbc:3.20.0'
    compileOnly 'com.github.webbukkit:DynmapCoreAPI:v2.5'
    compileOnly 'br.net.fabiozumbi12.UltimateChat:UltimateChat-Sponge-7:1.9.1'
    annotationProcessor 'org.spongepowered:spongeapi:7.3.0'

    testImplementation project(':EagleFactionsAPI')
    testImplementation 'org.spongepowered:spongeapi:7.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation 'org.mockito:mockito-core:3.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.10.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
}

shadowJar {
    archiveClassifier = ''
}

build.finalizedBy shadowJar
jar.enabled = false

test {
    useJUnitPlatform()
}

blossom {
    if(System.getenv("JENKINS_HOME")) {
        rootProject.version = version + "_" + System.getenv("BUILD_NUMBER") + "-SNAPSHOT"
        println("File name => " + archivesBaseName)
    } else {
        rootProject.version = version + "-SNAPSHOT"
    }
    replaceTokenIn('src/main/java/io/github/aquerr/eaglefactions/PluginInfo.java')
    replaceToken('%VERSION%', rootProject.version)
}

tasks.create("publishBuildOnDiscord") {
    group = "Publishing"
    description = "Task for publishing the jar file to discord's jenkins channel"
    doLast {
        String[] jarFiles = new FileNameFinder().getFileNames(project.buildDir.getPath(), "**/*.jar")

        if(jarFiles.length > 0) {
            println("Found jar files: " + jarFiles)

            String lastCommitDescription = "git log -1 --pretty=%B".execute().getText()
            if(lastCommitDescription == null || lastCommitDescription == "") {
                lastCommitDescription = "No changelog provided"
            }

            exec {
                commandLine("java", "-jar", "..\\jenkinsdiscordbot-1.0.jar", "EagleFactions", jarFiles[0], lastCommitDescription)
            }
        }
    }
}
